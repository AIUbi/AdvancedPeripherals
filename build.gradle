buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.0.20', changing: true
        classpath 'com.android.tools.build:gradle:2.3.0'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'java-library'

apply plugin: 'net.minecraftforge.gradle'

wrapper {
    gradleVersion = '7.1'
    distributionType = Wrapper.DistributionType.ALL
}

version = "${mod_version}"
group = 'de.srendi.advancedperipherals'
archivesBaseName = 'advancedperipherals'

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {

    mappings channel: "${mappings_channel}", version: "${mappings_version}"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            properties 'forge.logging.console.level': 'debug'

            mods {
                advancedperipherals {
                    source sourceSets.main
                }
            }
            if (project.hasProperty('mc_uuid')) {
                args '--uuid', project.getProperty('mc_uuid')
            }
            if (project.hasProperty('mc_username')) {
                args '--username', project.getProperty('mc_username')
            }
            if (project.hasProperty('mc_accessToken')) {
                args '--accessToken', project.getProperty('mc_accessToken')
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            properties 'mixin.env.disableRefMap': 'true'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            property 'forge.logging.console.level', 'debug'

            mods {
                advancedperipherals {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'advancedperipherals', '--all',
                    '--existing', file('src/main/resources').toString(),
                    '--existing', file('src/generated/resources').toString(),
                    '--output', file('src/generated/resources/')

            mods {
                advancedperipherals {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    maven { url 'https://squiddev.cc/maven/' }
    maven { url 'https://modmaven.dev/' }
    maven { url 'https://dvs1.progwml6.com/files/maven/' }
    maven { url 'https://repo.refinedmods.com/' }
    maven { url 'https://maven.theillusivec4.top/' }
    maven { url 'https://maven.blamejared.com' }
    maven { url 'https://www.cursemaven.com' }
    maven {
        url 'https://ldtteam.jfrog.io/ldtteam/modding'
    }
}

dependencies {
    // Minimal requirements
    compileOnly "org.jetbrains:annotations:${jb_annotations}"
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation fg.deobf("org.squiddev:cc-tweaked-${cc_version}")
    // Compile-time API required
    compileOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}:api")
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}:api")
    // Minimal requirements end

    // Extended requirements
    // Curios
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}")
    // Patchouli
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}")

    // Refined Storage
    implementation fg.deobf("com.refinedmods:refinedstorage:${rs_version}")
    // AE2
    implementation fg.deobf("appeng:appliedenergistics2:${ae_version}");

    // Minecolonies
    compileOnly fg.deobf("com.minecolonies:minecolonies:${minecolonies_version}:universal")
    compileOnly fg.deobf("com.ldtteam:structurize:${structurize_version}:universal")

    // Botania with deps
    compileOnly fg.deobf("vazkii.botania:Botania:${botania_version}")

    // Integrated dynamics
    compileOnly fg.deobf("curse.maven:integrateddynamics-${integrated_dynamics_version}")
    compileOnly fg.deobf("curse.maven:CommonCapabilites-${common_capabilities_version}")
    compileOnly fg.deobf("curse.maven:CyclopsCore-${cyclops_core_version}")

    // Mekanism with addons
    compileOnly fg.deobf("mekanism:Mekanism:${mekanism_version}")
    compileOnly fg.deobf("mekanism:Mekanism:${mekanism_version}:generators")

    runtimeOnly fg.deobf("mekanism:Mekanism:${mekanism_version}")
    runtimeOnly fg.deobf("mekanism:Mekanism:${mekanism_version}:generators")
    // Immersive Engineering
    compileOnly fg.deobf("curse.maven:immersiveengineering-${immersive_engineering_version}")

    runtimeOnly fg.deobf("curse.maven:immersiveengineering-${immersive_engineering_version}")
    // Storage Drawers
    compileOnly fg.deobf("curse.maven:storagedrawers-${storagedrawers_version}")

    // Testing stuff
//    runtimeOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-1.16:${top_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_version}")

}

jar {
    manifest({
        attributes([
                "Specification-Title"     : "advancedperipherals",
                "Specification-Vendor"    : "Srendi",
                "Specification-Version"   : "${version}",
                "Implementation-Title"    : "advancedperipherals",
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "Srendi",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    })
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}